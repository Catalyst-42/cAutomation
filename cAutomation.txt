/* cAutomation.txt - a main automation script
 * -> Created by Catalyst
 * -> v1.6
 * -> import cAutomation
 * 
 * Uses GetSymbol detection on Mines and Plateau 
 * screen should be 72 on 25 symbols on this locations
 * 
 * Loadouts & Weapons:
 * 1: L vigor wand dL +dps,   R vigor wand dL +dps
 * 2: L ice wand dI +dps,     R ice wand D +dps
 * 3: L ice sword dI +dps,    R ice sword dI +dps
 * 4: L fire sword D +dps,    R fire sword D +dps
 * 5: L poison sword dP +dps, R poison sword D +dps
 * 6: L vigor sword dL +dps,  R vigor sword D +dps
 * 7: L aether sword dU +dps, R aether sword D +dps
 *
 * Script uses:
 * F8ST - compound shield +def./sec.
 */

disable loadout print
disable pause

// The icon of automatization
>h-1,1,#fff,└#┘

// #region Эффекты
var debuffChill = 0
var debuffDot = 1
var debuffDot2 = 2
var debuffDamage = 3
var debuffStun = 4

var foeEffectsValues = [0, 0, 0, 0, 0]
var foeEffectsTimes = [0, 0, 0, 0, 0]
var foeEffectsMaxTimes = [0, 0, 0, 0, 0]
var foeEffectsNames = ["debuff_chill:", "debuff_dot:", "debuff_dot_2:", "debuff_damage:", "stun:"]
var foeEffectsIcons = ["❄", "φ", "φ", "∞", "o"]
var effectsList
var eString
eString = foe.debuffs.string
effectsList = string.Split(foe.debuffs.string, ",")

for foeBuffName = 0..foeEffectsNames.Count()-1
  ?eString = foeEffectsNames[foeBuffName]
    for i = 0..effectsList.Count()-1
      ?string.Split(effectsList[i], ":")[1] + ":" = foeEffectsNames[foeBuffName]
        ?foeEffectsValues[foeBuffName] ! int.Parse(string.Split(effectsList[i], ":")[2])
          foeEffectsMaxTimes[foeBuffName] = int.Parse(string.Split(effectsList[i], ":")[3]) + 1.0
        foeEffectsValues[foeBuffName] = int.Parse(string.Split(effectsList[i], ":")[2])
        foeEffectsTimes[foeBuffName] = int.Parse(string.Split(effectsList[i], ":")[3])
  :
    foeEffectsValues[foeBuffName] = 0
    foeEffectsTimes[foeBuffName] = 0
    foeEffectsMaxTimes[foeBuffName] = 0

/* Debug 
for i = foeEffectsNames.Count()-1..0
  >`8,@i+1@,@foeEffectsIcons[i]@:@foeEffectsValues[i]@:@foeEffectsTimes[i]@:@foeEffectsMaxTimes[i]@
>`23,1,>@string.Join("\n", string.Split(foe.buffs.string, ","))@\n
      ^>@string.Join("\n", string.Split(foe.debuffs.string, ","))@
*/
// #endregion

// Зелья
?loc.begin
  brew stone + bronze
  // brew wood + bronze

// Плато
?loc = Rocky
  var timer = 0
  ?foe.state = 32
    timer ++
  :
    timer = 0

?loc = Rocky & (ai.idle | [89, 90].Contains(timer)) & item.GetCooldown("bardiche") < 875
  loadout 4

  ?foeEffectsValues[debuffDot] = 0 & foe ! dysangelos_elementalist
    equipL fire sword dF -long

  ?hp < maxhp & foe.debuffs.count >= 5 & buffs.string ! lucky_crit
    equipL vigor sword dL

  ?foeEffectsValues[debuffChill] < 6 | foeEffectsTimes[debuffChill] <= 7 | 
  ^item.left = ice sword dI & foeEffectsTimes[debuffChill] < 300
    loadout 3

  ?(foeEffectsValues[debuffDamage] < 1 | foeEffectsTimes[debuffDamage] <= 14) & foe.state = 32 & foe = dysangelos_bearer
    equipL sword dP

  // ?totaltime > 30 & summon.count = 0
  //   equipL fire talisman
  //   activate L

  ?foe = acronian_scout
    equip arm

    ?foe.distance >= 16
      equipL triskelion
      equipR compound shield
    
  ?foe = dysangelos_elementalist  
    ?foe = poison
      equipL ice sword D

    ?foe = aether
      equipL vigor sword D

    ?foe = fire
      equipL aether sword D

    ?foe = vigor
      equipL poison sword D

    ?foe = ice
      equipL fire sword D

  ?foe = dysangelos_perfected
    ?summon.count ! 0 & summon.GetVar("ignition") > 2
      activate cinderwisp
    
    ?foeEffectsValues[debuffDot] = 0
      equipL fire sword dF -long

    ?foeEffectsValues[debuffChill] = 6 & foeEffectsValues[debuffDot] > 0 &
    ^buffs.string = lucky_crit
      loadout 4

    ?foeEffectsValues[debuffDot2] = 0 & timer >= 89
      equipR shield af

  ?[100, 101, 102, 117, 118, 126, 127, 124].Contains(foe.state)
    equipR compound shield
    ?armor < 14
      equipR F8ST

  ?foe = dysangelos_perfected
    ?foe.hp < 4400 & hp ! maxhp
      loadout 6

    draw.Bg(46, 8, #555)
    ?draw.GetSymbol(46, 8) = "/"
      loadout 3

// Лес
:?loc = Deadwood & (ai.idle | item.left = stone | item.right = quarterstaff | item.right = blade) & item.GetCooldown("bardiche") < 875
  equipL fire sword D -long
  equipR fire sword dF -long

  ?foe = mosquito & hp ! maxhp
    loadout 1

  ?foe = wasp | foe = wasp_nest
    loadout 2

  ?foe = Xyloalgia
    ?foeEffectsValues[debuffChill] < 6 | foeEffectsTimes[debuffChill] <= 7
      loadout 3
    :
      loadout 4

    ?foeEffectsValues[debuffDot] = 0
      equipR fire sword dF -long

    ?foe.distance >= 20
      equipL triskelion
  
  ?foe = poena
    equip bardiche
    ?foe.hp <= 180
      equip arm

// Пещеры
:?loc = Caves & item.GetCooldown("bardiche") < 875
  loadout 2
  ?foe.distance > 16
    equipL triskelion
  
  ?foe = cool_bat
    draw.Bg(33, 5, #555)

    ?foe.distance > 14
      equipL sword dF
    :
      equipL ice wand

    ?foe.debuffs.count >= 7
      loadout 2

    ?foe.distance <= 3
      equipL ice sword dI
      // equipR ice sword D
      equipR torch

  ?foe = spider_boss & foe.hp < foe.maxhp
    activate potion

    ?foe.hp <= 1600
      equipL ice sword dI
      equipR ice sword D

    // ?foeEffectsValues[debuffDot] < 1
    //  equipL fire wand

  ?hp ! maxhp
    loadout 1
  
// Грибы
:?loc = Mushroom & (ai.idle | item.left = stone | item.right = quarterstaff | item.right = blade) & item.GetCooldown("bardiche") < 875
  loadout 2

  // ?hp ! maxhp
  //   loadout 1

  // ?foe.distance <= 7 | (foe = boss & foe ! fluff & foe ! epic_snail)
  //   equip bardiche  

  ?foe = mushroom_boss
    equipR compound shield
    ?foeEffectsValues[debuffChill] < 6 | foeEffectsTimes[debuffChill] <= 7
      loadout 3
    :
      loadout 5

  ?foe = snail & foe = boss
    // equip bardiche
    
    ?foeEffectsValues[debuffChill] < 1
      equipL ice wand
      equipR poison wand  
  
  ?foe = fluff
    loadout 2
    
  ?buffs.string = lucky_crit
    equip bardiche
  
// Залы
:?loc = Halls & item.GetCooldown("bardiche") < 875
  var find_tomb = 0  
  loadout 2

  ?foe = boss & foe = ghost_tomb 
    loadout 6

  ?foe ! immune_to_physical & foe.distance < 6
    loadout 6

  ?foe = Pallas
    ?foeEffectsValues[debuffChill] < 6 | foeEffectsTimes[debuffChill] <= 7
      loadout 3
    :
      loadout 6

    ?foeEffectsValues[debuffDamage] < 1
      loadout 5
    
    ?foe.count > 2
      equip arm

  // ?buffs.string = lucky_crit
    // equip bardiche

// Шахта
:?loc = Mine & (ai.idle | (draw.GetSymbol(21, 18) = "/" & foe = bronze_guardian | item.right = quarterstaff) | item.left = stone) & item.GetCooldown("bardiche") < 875
  equipL aether wand
  equipR shield au

  ?foe = bronze_guardian & foe = boss
    draw.Bg(21, 19, #555)
    ?draw.GetSymbol(21, 19) = "/"
      loadout 7
    :
      equip aether crossbow

  ?foe = bomb_cart & foe = boss
    ?foe.distance < 30
      equip repeating crossbow
    :
      equipL triskelion
      equipR shield au

  ?foe = boss & foe.state = 1
    equipL quest
    equipR compound shield

// Хребет
:?loc = Ridge & (ai.idle | item.left = stone | item = bardiche | item.right = quarterstaff | item.right = blade) & item.GetCooldown("bardiche") < 875
  equipR shield au
  equipL fire sword

  ?hp ! maxhp
    equipL vigor wand

  ?foe = ice_pillar
    equipL aether sword dU -long
    equipR shiny aether sword dU -long

  ?(foe = boss)
    loadout 4

    ?foe = yeti
      ?(foeEffectsValues[debuffChill] < 6 | foeEffectsTimes[debuffChill] <= 7) & (foe = yeti)
        loadout 3
      :
        ?foe.distance < 9
          loadout 4
        :
          equipR shield au
          equipL triskelion

      ?foe.distance > 30
        equipL triskelion
        equipR shield au
      
// Храм
:?loc = Temple
  ?foe = nagaraja
    draw.Bg(22, 12, #555)
    draw.Bg(25, 15, #555)
    draw.Bg(31, 12, #555)

  ?(draw.GetSymbol(22, 12) = "(" | draw.GetSymbol(25, 15) = "(" | draw.GetSymbol(31, 12) = "(")
    equip quarterstaff
    activate R

  ?loc = Temple & (ai.idle | item.left = stone | item.right = quarterstaff | item.right = blade) & item.GetCooldown("bardiche") < 875
    loadout 2

    ?hp ! maxhp
      loadout 1

    ?foe = acronian_cultist
      ?foeEffectsValues[debuffChill] < 6 | foeEffectsTimes[debuffChill] <= 7
        equip ice staff dI
      :
        equip ice crossbow D
      
      ?item.GetCooldown("bardiche") > 0
        equipL ice wand D
        equipR ice sword D -long

    ?foe = nagaraja
      ?foeEffectsValues[debuffChill] < 6 | foeEffectsTimes[debuffChill] <= 10
        equip ice staff dI
      :
        equip ice crossbow D
        activate potion

      ?foe.distance < 4
        equipL ice sword dI -long
        equipR ice sword D -long
      
      ?foe.distance > 19
        equipL triskelion
        equipR compound shield

      ?foe.state = 1
        equipR compound shield
        equipL quest
