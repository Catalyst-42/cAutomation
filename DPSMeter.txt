//============DPS_Checker============//
//made by Pikarizard Charikachu xD
//v1.0
//
//DESCRIPTION
//Displays various stats to help you in 
//min-maxing your damage output. Only 
//works on bosses. Also coded so that it 
//only records stats the moment you 
//damage bosses. Neat right? xD
//
//INSTRUCTIONS
//For PC or if included in the game:
//  Default:
//    import DPS_Checker
//  If you want to customize it:
//
//    var DPSC //you can name this 
//               anything
//
//    DPSC = import DPS_Checker
//    DPSC.APC = "#white" //Text Color
//    DPSC.APX = 0        //x offset
//    DPSC.APY = 6        //y offset
//
//On mobile:
//  Copy paste this into your mindstone
//  Change the user config variables 
//  below as needed instead
//
//Have fun! xD
//
//============xD=5SGL=:D==============//

//User configurable variables for the ASCII Print:

var APC = "#white"
var APX = 20
var APY = 6

//Global Variables:

var TT
var HPA = 0
var HAM = 0
var F

TT  = totaltime
HPA = foe.hp + foe.armor
HAM = foe.maxhp + foe.maxarmor
F   = foe

var Hits = 0
var DMG  = 0
var DMGS = 0
var T2   = 0
var HPA2 = 0

var FRM
var FPH
var HPS
var DPF
var DPS

?F = "boss"
  ?HPA < HAM & HPA > 0
    ?HPA2 > HPA
      Hits += 1
      DMG   = HPA2 - HPA
      DMGS  = DMGS + DMG
  :
    T2   = TT
    Hits = 0
    DMG  = 0
    DMGS = 0  
  FRM = TT   - T2
  FPH = FRM  / (Hits-1) // -1 is there to avoid stat flicking)
  HPS = 30   / FPH
  DPF = DMGS / FRM
  DPS = DPF  * 30 
  >`@APX@,@APY@,@APC@,ascii
Stats:
-Frames:####@FRM@
-Hits:######@Hits@
-Hit#Delay:#@FPH@#Frame/s
-Hits/Sec:##@HPS@#/30
-DMG#Dealt:#@DMGS@
-DMG#/Hit:##@DMG@
-DMG#/Frame:@DPF@
-DMG#/Sec:##@DPS@
asciiend

HPA2 = HPA
