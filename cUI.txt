/* cUI.txt - a copy of the bottom UI of the player and foes
 * -> Created by Catalyst
 * -> v1.0
 * -> import cUI
 */

disable hud

// UI
var UIfoe
var UImain
?loc.begin | time = 1
  UImain = ui.AddText()
  UImain.visible = true

  UImain.w = screen.w
  UImain.h = 1
  UImain.y = screen.h - 1

  UImain.dx = left
  UImain.dy = top
  UImain.ax = left
  UImain.ay = top

  UIfoe = ui.AddText()
  UIfoe.visible = true

  UIfoe.w = screen.w
  UIfoe.h = 1
  UIfoe.y = screen.h - 1

  UIfoe.dx = left
  UIfoe.dy = top
  UIfoe.ax = left
  UIfoe.ay = top

// Hp
var UIHP = ""
var pervHP = hp
?pervHP - hp > 0
  UIHP = "[color=#000]" + hp + "[/color]"
:
  UIHP = "[color=#fff]" + hp + "[/color]"
pervHP = hp

// Armor
var UIarmor = ""
var pervArmor = armor + armor.f
?armor > 0 | armor.f > 0
  ?(pervArmor) - (armor * 10 + armor.f) > 5
    UIarmor = "[color=#000][" + armor + "." + armor.f + "][/color]"
  :
    UIarmor = "[color=#999][" + armor + "." + armor.f + "][/color]"
:
  UIarmor = ""
pervArmor = armor * 10 + armor.f

// Effects
var baseColor
func interpolate(x)
  ?x >= 100
    return 1.0
  return x / 100.0

var UIeffects = ""
var effectsValues = [0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0]
var effectsTimes = [0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0]
var effectsMaxTimes = [0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0]
var effectsNames = ["smite:", "bardiche_buff_aoe_chance:", 
                  "bardiche_buff_crit_chance:", "bardiche_buff_crit_mult:",    
                  "bardiche_buff_move_speed:", "berserk:", "lucky_crit:", 
                  "lucky_mult:", "strength:", "invisibility:", "vampiric:", 
                  "experience:", "quarterstaff_buff_attack_speed:", "quarterstaff_buff_stun:", "pick_pocket:", 
                  "debuff_duration_damage:", "debuff_damage:", "debuff_dot:", "debuff_chill:", 
                  "debuff_yeti_chill", "debuff_move_speed:", "debuff_attack_speed:", "stun:"]
var effectsIcons = ["•", "o", "o", "o", "o", "ʘ", "!", "x", "o", "?", "W", "@", "o", "o", "≡",
                    "∞", "∞", "φ", "❄", "❄", "❄", "❄", "o"]

var effectsList
var eString
eString = buffs.string
effectsList = string.Split(buffs.string, ",")
for buffName = 0..effectsNames.Count()-1
  ?buffName > 14
    eString = debuffs.string
    effectsList = string.Split(eString, ",")
  ?eString = effectsNames[buffName]
    for i = 0..effectsList.Count()-1
      ?string.Split(effectsList[i], ":")[1] + ":" = effectsNames[buffName]
        ?effectsValues[buffName] ! int.Parse(string.Split(effectsList[i], ":")[2])
          effectsMaxTimes[buffName] = int.Parse(string.Split(effectsList[i], ":")[3]) + 1.0
        effectsValues[buffName] = int.Parse(string.Split(effectsList[i], ":")[2])
        effectsTimes[buffName] = int.Parse(string.Split(effectsList[i], ":")[3])
  :
    effectsValues[buffName] = 0
    effectsTimes[buffName] = 0
    effectsMaxTimes[buffName] = 0

UIeffects = ""
for i = 0..effectsValues.Count()-1
  ?i = 0 // smite
    baseColor = "#f0f"
  :?i > 14 // debuffs
    baseColor = "#f00"
  :
    baseColor = "#00f"

  ?effectsValues[i] ! 0
    ?effectsValues[i] = 1
      UIeffects = UIeffects + "[color=" + 
                  ^color.Lerp("#000", baseColor, effectsTimes[i] / effectsMaxTimes[i]) + "]" + 
                  ^effectsIcons[i] + "[/color]"
    :
      UIeffects = UIeffects + "[color=" + baseColor + "]" + effectsValues[i] + "[/color][color=" + 
                  ^color.Lerp("#000", baseColor, effectsTimes[i] / effectsMaxTimes[i]) + 
                  ^"]" + effectsIcons[i] + "[/color]"

// Build UI line
?ai.enabled
  UImain.text = "#└O┘#" + UIHP + "/" + maxhp + UIarmor + "#" + UIeffects
:
  UImain.text = ""

/* debug
for i = 6..7 // effectsNames.Count()-1
  >`8,@i+1-6@,@effectsIcons[i]@:@effectsValues[i]@:@effectsTimes[i]@:@effectsMaxTimes[i]@
>`23,0,>@string.Join("\n", string.Split(buffs.string, ","))@\n
^>@string.Join("\n", string.Split(debuffs.string, ","))@
*/

// Foe
var foePartUILength = 0
foePartUILength = 0
var foeArmor = ""
?foe.armor > 0
  foeArmor =  "[color=#999][" + foe.armor + "][/color]"
  foePartUILength += string.Size("[" + foe.armor + "]")
:
  foeArmor = ""

// Foe effects
var foeUIeffects = ""
var foeEffectsValues = [0, 0, 0, 0, 0, 0, 0, 0, 0]
var foeEffectsTimes = [0, 0, 0, 0, 0, 0, 0, 0, 0]
var foeEffectsMaxTimes = [0, 0, 0, 0, 0, 0, 0, 0, 0]
var foeEffectsNames = ["poena_mirror:", "spider_buff_damage:", "buff_protection:",
                      ^"debuff_feeble:", "debuff_chill:", "debuff_dot:", "debuff_dot_2:", "debuff_damage:","stun:"]
var foeEffectsIcons = ["♀", "∞", "♥", "∞", "❄", "φ", "φ", "∞", "o"]
var effectsList
eString = foe.buffs.string
effectsList = string.Split(foe.buffs.string, ",")
for foeBuffName = 0..foeEffectsNames.Count()-1
  ?foeBuffName >= 3
    eString = foe.debuffs.string
    effectsList = string.Split(eString, ",")
  ?eString = foeEffectsNames[foeBuffName]
    for i = 0..effectsList.Count()-1
      ?string.Split(effectsList[i], ":")[1] + ":" = foeEffectsNames[foeBuffName]
        ?foeEffectsValues[foeBuffName] ! int.Parse(string.Split(effectsList[i], ":")[2])
          foeEffectsMaxTimes[foeBuffName] = int.Parse(string.Split(effectsList[i], ":")[3]) + 1.0
        foeEffectsValues[foeBuffName] = int.Parse(string.Split(effectsList[i], ":")[2])
        foeEffectsTimes[foeBuffName] = int.Parse(string.Split(effectsList[i], ":")[3])
  :
    foeEffectsValues[foeBuffName] = 0
    foeEffectsTimes[foeBuffName] = 0
    foeEffectsMaxTimes[foeBuffName] = 0
foeUIeffects = ""
for i = foeEffectsValues.Count()-1..0
  ?i >= 3 // buffs
    baseColor = "#f00"
  :
    baseColor = "#00f"

  ?foeEffectsValues[i] ! 0
    ?foeEffectsValues[i] = 1
      foeUIeffects += "[color=" + 
                      ^color.Lerp("#000", baseColor, foeEffectsTimes[i] / foeEffectsMaxTimes[i]) + "]" + 
                      ^foeEffectsIcons[i] + "[/color]"
      foePartUILength += string.Size(foeEffectsIcons[i])
    :
      foeUIeffects += "[color=" + baseColor + "]" + foeEffectsValues[i] + "[/color][color=" + 
                      ^color.Lerp("#000", baseColor, foeEffectsTimes[i] / foeEffectsMaxTimes[i]) + 
                      ^"]" + foeEffectsIcons[i] + "[/color]"
      foePartUILength += string.Size(foeEffectsValues[i] + foeEffectsIcons[i])

// Build foe UI line
?ai.enabled = false | !foe | foe.distance > 30 | time <= 5
  UIfoe.text = ""
:
  UIfoe.x = screen.w - foePartUILength - string.Size("#" + foe.hp + "/" + foe.maxhp + "#" + te.xt(foe.name) + "#")
  UIfoe.text = foeUIeffects + "#" + foeArmor + foe.hp + "/" + foe.maxhp + "#" + te.xt(foe.name) + "#"

/* debug
for i = foeEffectsNames.Count()-1..0
  >`8,@i+1@,@foeEffectsIcons[i]@:@foeEffectsValues[i]@:@foeEffectsTimes[i]@:@foeEffectsMaxTimes[i]@
>`23,1,>@string.Join("\n", string.Split(foe.buffs.string, ","))@\n
      ^>@string.Join("\n", string.Split(foe.debuffs.string, ","))@
*/
