var utils = import c/Utils

var wandLeft = "vigor wand +21 D shiny"
var wandRight = "vigor wand +21 D -shiny"

var casualLeft =  "vigor sword +21 D  shiny -long"
var casualRight = "vigor sword +21 D -shiny -long"

var iceLeft =  "ice sword dI  shiny -long"
var iceRight = "ice sword dI -shiny -long"

utils.potion("lucky")
utils.walk(16)

?totaltime > 10 & totaltime < 40
  utils.aether()

?screen.i = 1 &
^foe.distance < 8
  activate voidweaver

?foe.distance < 12
  ?screen.i = 1 & foe ! pallas
    ?foe.count = 2
      utils.bardiche()
    :
      equipL @wandLeft@
      equipR @wandRight@

  ?foe = pallas
    ?foe = phase2
      ?foe.count > 1
        equipL @wandLeft@
        equipR @wandRight@
      :?item.GetCooldown("bardiche") <= 0 |
       ^item.GetCooldown("bardiche") >= 875
        utils.bardiche()
      :?foe.debuffs.GetCount("debuff_chill") < 6
        utils.moondial(iceLeft, iceRight)
      :
        utils.moondial(casualLeft, casualRight)
    :
      activate potion
      utils.moondial(casualLeft, casualRight)

utils.wait()
utils.collect()
utils.dash()
