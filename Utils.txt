var bladeTimeout = 20

func flush(x, y, sign)
  >`@screen.w - x@,@y@,#f0f,@sign@


func walk(dist)
  ?foe.distance <= dist
    return
  
  ?hp ! maxhp 
    equipL ouroboros
  :
    equipL triskelion
  
  ?foe.distance > 20 & foe.distance < 25
    ?foe = poison
      equipR ice shield A
    :?foe = vigor
      equipR poison shield A
    :?foe = aether
      equipR vigor shield A
    :?foe = fire
      equipR aether shield A
    :?foe = ice
      equipR fire shield A
    :
      equipR ice shield ai

  :?foe.distance >= 25
    ?armor < 13
      equipR compound shield F8ST
    :
      equipR compound shield N2X24


func wait()
  ?ai.paused
    equipL quest
    equipR compound shield N2X24


func collect()
  ?pickup & pickup.distance <= 16
    equipR star


// Summons
func aether()
  ?summon.GetId() ! "voidweaver" &
  ^foe ! boss
    equipR aether talisman
    equipL triskelion
    activate R

func fire() 
  ?summon.GetId() ! "cinderwisp" &
  ^foe = boss
    equipR fire talisman
    equipL triskelion
    activate R

// Defensive abilities
func mind(dist)
  ?item.GetCooldown("mind") <= 0 &
  ^foe.distance < dist
    flush(4, 1, "M")
    equipR mind stone
    ?item.CanActivate()
      activate R


func quarterstaff()
  ?time <= 30 | ai.paused
    return

  ?foe.distance > 16 &
  ^item.GetCooldown("quarterstaff") = 0 &
  ^item.CanActivate(m)
    flush(4, 1, "Q")
    equip quarterstaff
    ?item.CanActivate()
      activate R


func dash()
  ?foe.distance >= 11 &
  ^foe.distance <= 16
    ?item.GetCooldown("bash") <= 0
      flush(4, 1, "B")
      equipR bashing shield
    :?item.GetCooldown("dash") <= 0
      flush(4, 1, "D")
      equipR dashing shield


// Frameskipping
func aac(rightItem, leftItem)
  ?foe.distance > 6
    return

  ?item.right.state = 3
    flush(1, 1, "R")
    equipR *10 +21 sword  // Trash stub
    equipR @rightItem@

  ?item.left.state = 3
    flush(2, 1, "L")
    equipL *10 +21 sword  // Trash stub
    equipL @leftItem@


func moondial(itemLeft, itemRight)
  ?totaltime % 3 = 1
    flush(1, 1, "R")
    equipL moon
    equipR @itemRight@

  :?totaltime % 3 = 2
    equipL moon
    flush(1, 1, "L")
    equipR @itemLeft@

  :
    flush(2, 1, "LR")
    equipL @itemLeft@
    equipR @itemRight@


// Offensive abilities
func hammer()
  ?item.GetCooldown("hammer") <= 0 & item.CanActivate()
    flush(3, 1, "H")
    equip heavy hammer
    activate R


func blade()
  ?item.GetCooldown("blade") <= 0 &
  ^foe.distance < 30
    screen.next()
    bladeTimeout--

    ?bladeTimeout = 0
      flush(3, 1, "B")
      equip blade
      activate R
      
      screen.ResetOffset()
      bladeTimeout = 20


func bardiche()
  ?(item.GetCooldown("bardiche") = 0 | item.GetCooldown("bardiche") >= 875) &
  ^foe.distance <= 11
    flush(3, 1, "R")
    equip bardiche shiny
    activate R
