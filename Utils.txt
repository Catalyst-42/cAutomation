var bladeTimeout = 20

func flush(x, y, sign)
  >`@screen.w - x@,@y@,#00f,@sign@


// Potions
func potion(type)
  ?!loc.begin
    return
  
  ?type = "strength"
    brew stone
  :?type = "experience"
    brew wood
  :?type = "healing"
    brew tar
  :?type = "lightning"
    brew bronze
  :?type = "vampiric"
    brew tar + bronze
  :?type = "lucky"
    brew stone + bronze
  :?type = "berserk"
    brew wood + bronze
  :?type = "cleansing"
    brew wood + tar
  :?type = "defensive"
    brew stone + tar
  :?type = "invisibility"
    brew wood + stone


// Moveset
func wait()
  ?ai.paused
    flush(4, 1, "-")

    equipL quest
    equipR compound shield N2X24

  // ?ai.paused
  //   equipL triskelion
  //   equipR mask


func collect()
  ?pickup & pickup.distance <= 16
    equipR star


func quarterstaff()
  ?time >= 30 &
  ^!ai.paused &
  ^foe.distance > 16 &
  ^item.GetCooldown("quarterstaff") = 0 &
  ^item.CanActivate()
    flush(4, 1, "Q")
    equip quarterstaff
    ?item.CanActivate()
      activate R


func walk(dist)
  ?foe.distance <= dist
    return
  
  flush(4, 1, "+")

  // ?hp ! maxhp 
  //   equipL ouroboros
  // :
  equipL triskelion
  
  ?foe.distance > 20 & foe.distance < 25
    ?foe = poison
      equipR ice shield A
    :?foe = vigor
      equipR poison shield A
    :?foe = aether
      equipR vigor shield A
    :?foe = fire
      equipR aether shield A
    :?foe = ice
      equipR fire shield A
    :
      equipR ice shield ai

  :
    ?armor < 13
      equipR compound shield F8ST
    :
      equipR compound shield N2X24


// Summons
func aether()
  ?summon.GetId() ! "voidweaver"
    equipR aether talisman
    equipL triskelion
    activate R


func fire() 
  ?summon.GetId() ! "cinderwisp"
    equipR fire talisman
    equipL triskelion
    activate R


// Defensive abilities
func mind(dist)
  ?item.GetCooldown("mind") <= 0 &
  ^foe.distance < dist
    flush(4, 1, "M")
    equipR mind stone
    ?item.CanActivate()
      activate R


func dash()
  ?foe.distance >= 11 &
  ^foe.distance <= 16
    ?item.GetCooldown("dash") <= 0
      flush(4, 1, "D")
      equipR dashing shield
    :?item.GetCooldown("bash") <= 0
      flush(4, 1, "B")
      equipR bashing shield
      

func bash()
  ?foe.distance >= 11 &
  ^foe.distance <= 16
    ?item.GetCooldown("bash") <= 0
      flush(4, 1, "B")
      equipR bashing shield
    :?item.GetCooldown("dash") <= 0
      flush(4, 1, "D")
      equipR dashing shield


// Offensive abilities
func hammer()
  ?item.GetCooldown("heavy_hammer") <= 0 &
  ^item.CanActivate()
    flush(3, 1, "H")
    equip heavy hammer shiny
    activate R


func blade()
  ?item.GetCooldown("blade") <= 0 &
  ^foe.distance < 30
    screen.next()
    bladeTimeout--

    ?bladeTimeout = 0
      flush(3, 1, "B")
      equip blade
      activate R
      
      screen.ResetOffset()
      bladeTimeout = 20


func mask()
  ?item.GetCooldown("mask") <= 0 &
  ^item.CanActivate()
    equipR mask
    activate R


func bardiche()
  ?(item.GetCooldown("bardiche") = 0 |
  ^item.GetCooldown("bardiche") >= 875) &
  ^foe.distance <= 11
    flush(3, 1, "R")
    equip bardiche shiny
    activate R


// Frameskipping
func arm()
  ?totaltime % 5 = 0
    equipR *0 +0 sword  // Trash stub
    equipL triskelion
  :
    equip skeleton arm


func aac(rightItem, leftItem)  // ? Useful ?
  ?foe.distance > 6
    return

  ?item.right.state = 3
    flush(1, 1, "R")
    equipR *0 +0 sword  // Trash stub
    equipR @rightItem@

  ?item.left.state = 3
    flush(2, 1, "L")
    equipL *0 +0 sword  // Trash stub
    equipL @leftItem@


func aacR(rightItem)  // ? Useful ?
  equip @rightItem@
  
  ?item.right.state = 3
    flush(1, 1, "R")
    equipR *0 +0 sword  // Trash stub
    equip @rightItem@


func aacL(rightItem)  // ? Useful ?
  equip @rightItem@
  
  ?item.right.state = 3
    flush(2, 1, "L")
    equipL *0 +0 sword  // Trash stub
    equipL @rightItem@
    return true
  :
    return false


func moondial(itemLeft, itemRight)
  ?totaltime % 3 = 0
    flush(2, 1, "LR")
    equipL @itemLeft@
    equipR @itemRight@
  
  :?totaltime % 3 = 1
    flush(1, 1, "R")
    equipL moon
    equipR @itemRight@

  : // 2
    flush(1, 1, "L")
    equipL moon
    equipR @itemLeft@
